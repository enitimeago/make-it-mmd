blendshape_viewer_license = """\\\r
// Blendshape Viewer\r
//\r
// MIT License\r
//\r
// Copyright (c) 2023 Ha\303\257~ (@vr_hai github.com/hai-vr)\r
//\r
// Permission is hereby granted, free of charge, to any person obtaining a copy\r
// of this software and associated documentation files (the "Software"), to deal\r
// in the Software without restriction, including without limitation the rights\r
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell\r
// copies of the Software, and to permit persons to whom the Software is\r
// furnished to do so, subject to the following conditions:\r
//\r
// The above copyright notice and this permission notice shall be included in all\r
// copies or substantial portions of the Software.\r
//\r
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR\r
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,\r
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE\r
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER\r
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,\r
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE\r
// SOFTWARE.\r
"""

core.workflow(
    name = "blendshape-viewer",
    origin = git.origin(
        url = "https://github.com/hai-vr/blendshape-viewer-vcc.git",
        ref = "main",
    ),
    destination = git.github_pr_destination(
        url = "https://github.com/enitimeago/make-it-mmd.git",
        destination_ref = "copybara",
        title = "Update Blendshape Viewer",
        update_description = True,
        pr_branch = "update-blendshape-viewer",
    ),
    destination_files = glob(["third_party/blendshape-viewer/**"]),
    authoring = authoring.pass_thru("github-actions[bot] <41898282+github-actions[bot]@users.noreply.github.com>"),
    transformations = [
        # Prepend license header to all source files.
        # Need to handle UTF-8 BOM otherwise it incorrectly gets put after the header.
        # Seems like it can't be matched directly via regex so use a workaround.
        core.replace(
            before = "${utf8_bom}${first_character}",
            after = "${utf8_bom}" + blendshape_viewer_license + "${first_character}",
            regex_groups = {
                "utf8_bom": r"^.*?",
                "first_character": r"[a-zA-Z/#]",
            },
            paths = glob(["**/*.compute", "**/*.cs", "**/*.shader"]),
            first_only = True,
            multiline = True,
        ),
        # Rename namespace to avoid collision with upstream.
        core.replace(
            before = "Hai.BlendshapeViewer",
            after = "enitimeago.NonDestructiveMMD.vendor.BlendshapeViewer",
            paths = glob(["**/*.cs"]),
        ),
        core.move("", "third_party/blendshape-viewer"),
    ],
)
